 component←(tn _append)data;at;unused;reserved
⍝ Logically append a new data component to the file (reuse free space if available)

 :If 0≠at←1⊃(tn _find ¯1)⎕UCS 127 ⍝ Do we already have a free-space component?
 :AndIf ×≢unused←⎕FREAD tn,at ⍝ And it's not empty
     component←⊢/unused ⋄ (unused←¯1↓unused)⎕FREPLACE tn,at ⍝ Use the LIFO component # and remove it from the free-list
     :If component<0 ⍝ Did we run into a sub-list pointer?
         unused,←⎕FREAD tn,component←-component ⍝ Move sub-list into main list; Reuse sub-list component for new data
         unused ⎕FREPLACE tn,at ⍝ Re-update the main list
     :EndIf
     data ⎕FREPLACE tn,component ⍝ Write their data to the acquired component #
 :Else ⍝ Nothing is free for reuse
     :If 0≠at←⊃(tn _find ¯1)⎕UCS 0 ⋄ reserved←⎕FREAD tn,at ⍝ Need to skip over any reservations?
         :While (2⊃⎕FSIZE tn)∊reserved ⋄ :Until 0=''⎕FAPPEND tn
     :EndIf
     component←data ⎕FAPPEND tn ⍝ Create a fresh component with the data
 :EndIf
